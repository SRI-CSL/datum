datum = line_prefix firstline newline lines

<firstline> = cfirstline | sfirstline
cfirstline = subject spaces assay spaces <'is'> spaces change spaces ctreatment

<ctreatment> = &'irt' changetype spaces treatments (spaces? stimes)? |
               &'by' changetype spaces treatments |
               &'itpo' changetype spaces treatments |
               &'itao' changetype spaces ktreatment

changetype = symbol
change = 'increased' | 'decreased' | ('detectable-but' spaces)? 'unchanged'
stimes = <'('> (stpt | excuse) <')'>
stpt = nat spaces symbol
excuse = 'tnr' | 'times'

sfirstline = subject spaces assay spaces <'is'> spaces schange
schange = 'undetectable' | 'detectable'

<lines> = datumline (spaces? newline datumline)*
<datumline> = <'  '> line_prefix
              (source | environment | times | extra |
               meta | comment | oligo | ipfrom) spaces?

(*
  A subject can be one of the following:
    - No subject at all
    - An oprotein with origin r/p/s
    - A protein with any other origin, plus a handle and IP
    - A chemical with a handle
    - A gene
*)
subject = <'NS'> /
          &#'[rps]' oprotein /
          chemical handle /
          oprotein handle ip /
          gene

chemical = simple_symbol
gene = simple_symbol
<simple_symbol> = !#'[xbrpks]' symbol

<xsprotein> = &'x' oprotein handle ip
ip = 'IP'?
handle = <'['> symbol <']'>

(*
  TODO - Document proteins
*)
oprotein = origin protein mutations modifications

origin = #'[xbrpks]?'
protein = symbol

mutations = (<'('> pmutation (<'/'> pmutation)* <')'> mutation_string?)?
<pmutation> = mutation | <'('> mutation <')'>
<mutation> = domain / del_mut / s_mut / point / symbol_mut
del_mut = <'del('> domain <')'>
s_mut = #'p[TSY]\d{1,4}'
point = letter nat letter
domain = nat <'-'> nat
symbol_mut = symbol
mutation_string = string

modifications = modification*
modification = <'{'> mod_symbol sites? <'}'>
mod_symbol = symbol
sites = <'('> site (<'/'> site)* <')'>
<site> = #'([ACTYSKPNLMVIFDERHQWG]|p[TYS])\d+' | symbol

(*
  TODO - Document assays
*)
assay = activity_assay / locatedin / infraction / boundto / phos / generic_assay / binding
detect = <'['> symbol <']'>
assay_sym = symbol

activity_assay = activity <'('> substrates <')'> sites? detect
activity = ('IVKA' | 'IVLKA' | 'IVGefA' | 'IVPPaseAct' | 'IVHatAct' | 'IVProteaseAct' | 'IVHDACAct')
substrates = symbol (<#'[/,]'> symbol)*

locatedin = &'locatedin' assay_sym <'('> position <')'> detect
infraction = &'infraction' assay_sym <'('> fraction <')'> detect
boundto = &'boundto' assay_sym <'('> gene <')'> detect
position = symbol
fraction = symbol
gene = symbol

phos = phostype &'phos' assay_sym detect
phostype = 'ST' | #'[STY]'

generic_assay = assay_sym sites? detect

binding = assay_sym detect spaces hooks
hooks = <'('> hook (plus hook)* <')'> | hook (plus hook)*
hook = (hook_entity / oprotein) handle ip
hook_entity = symbol

(*
  TODO - Document treatments
  treat_sym can be Antibody, Stress, or Chemical
*)
<treatment> = treat_sym / oprotein / xsprotein
treat_sym = symbol
<streatment> = treatment (spaces conjunction spaces treatment)*

conjunction = 'and/or' | 'and' | 'or' | '+'

treatments = <'('> spaces? streatment spaces? <')'> /
             streatment

ktreatment = treatment ktest (spaces conjunction spaces treatment ktest)* |
             treatment (spaces conjunction spaces treatment)* ktest

ktest = spaces <'['> symbol <']'>

(*
  TODO - Document environments
*)
environment = <'cells:'> spaces? env_description
ipfrom = <#'IP[12s]?from'> <':'> spaces? env_description;

<env_description> = env_none / cells cell_mutations spaces <'in'> spaces medium strings?

cells = symbol
medium = symbol
env_none = 'none'

strings = (spaces string)*

cell_mutations = cell_mutation*
cell_mutation = <'<'> (protein <'~'> mutation_type | oprotein) <'>'>
mutation_type = symbol

(*
  TODO - Document sources
*)
source = <'source:'> spaces? pmid <#'\([DR]\)'>? figures
pmid = nat
figures = (<'-'> ('Fig' | 'Table' | figs))*
<figs> = figure (<','> figure)*
<figure> = #'S?\d*[A-Za-z]?(\[[lrtbm]+\])?'

(*
  TODO - Document times
*)
times = <'times:'> spaces? timepoints spaces unit
timepoints = timepoint (spaces timepoint)*
timepoint = #'\d+' #'\+*'
unit = symbol

(*
  TODO - Document extras
*)
extra = extra_name <' by:'> spaces extra_treat |
        req_name <':'> spaces reqs_body

extra_name = 'enhanced' |
             'inhibited' |
             'repressed' |
             'reversed' |
             'unaffected'

<extra_treat> = streatment stim_times? spaces <'['> addition_mode <']'> |
                streatment spaces <'['> sub_mode <']'>

addition_mode = 'addition' /
                'cotreatment' /
                'omission' /
                'pretreatment' /
                'posttreatment' /
                symbol

stim_times = stim_times_list | stim_times_excuse
stim_times_list = numlist spaces unit
stim_time_excuse = 'tnr'
<numlist> = nat (spaces? <','> spaces? nat)*

sub_mode = 'substitution' /
           'knockin'

req_name = 'reqs' / 'does not req'
<reqs_body> = ktreatment |
              reqs_type spaces omission
reqs_type = 'both'
omission = <'['> #'omissions?' <']'>

(*
  TODO - Document oligos
*)
oligo = <'oligo:'> spaces oligo_body
<oligo_body> = oligo_str spaces? oligo_seq?
oligo_str = string | symbol
oligo_seq = #'5-[ATCG ]+-3' / symbol

(*
  TODO - Document meta/comments
*)
meta = <'meta:'> spaces? rest_of_line
comment = <'comment:'> spaces? rest_of_line

<string> = <'"'> #'[^"]*' <'"'>
<symbol> = #'[\w\.\?!\\\-]+'
<rest_of_line> = #'[^\n]*'
<spaces> = <#' +'>
<plus> = spaces <'+'> spaces
<nat> = #'\d+'
<letter> = #'[A-Za-z]'
<line_prefix> = <'  ***'> spaces
<newline> = <#'\n'>
